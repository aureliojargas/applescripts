(*

Export Contacts to Yahoo CSV (http://aurelio.net/bin/as)
	by Aurelio Marinho Jargas
	v1.2 - April 18, 2005
   
Apple Script to export the Address Book contacts to the Yahoo! CSV format.
Then they can be uploaded to the Yahoo Web-based Address Book.


Features:
	Exports all contacts or a selected group

	Exports ALL fields that are used by Yahoo Address Book, including groups.

	Correctly handles quote character escaping, international text and date formats.
	
	The four Yahoo custom fields are filled with IM numbers: ICQ, MSN, AIM and Jabber
    
Usage:
	Just run this script and it will save the "Yahoo-AB.csv" file to your Desktop.

Install:
	Save this script file under the "Library/Scripts/Address Book Scripts" folder,
	inside your personal folder ($HOME). Create this folder if necessary.


History:
	v1.0 March  9, 2005
		First public release
	v1.1 March 12, 2005
		MaxContacts set to 999, MaxContacts sanity check, AvailableFilePath()
	v1.2 April 18, 2005
		Group selection, Informative headers, Open in Finder/TextEdit, It-will-take-time message
   

Thanks:
	Rona Wronkeri (USA) for requesting the Group Selection.
	Damien Andrews (Australia) for typos fixes, informative headers suggestion.
   

Details:
	Note that Yahoo! has some storage limitations:
		3 e-mail addresses
		2 street addresses (home, work)
		1 fax number

	Yahoo! import road map:		
		Log in your Yahoo! Mail account
		Click "Addresses" tab (upper left)
		Click "Import/Export" link (upper right)
		Step 1) Choose "Yahoo! (.CSV file)"
		Step 3) Choose the file generated by this script
		Step 4) Click "Import Now" button
		Done! 

	Yahoo! successful importing tips:  
  		If possible, first remove all your previous contacts.
		The nickname cannot contain spaces or symbols, use just letters.
		The e-mails are checked, make sure they are valid.
		Duplicated nicknames are not allowed.
		Most fields are OK to be empty.
	
	Most fields are equivalent in both Address Books, but some haven't got a direct pair.
	The following is the complete list of corresponding fields used by this script:
	  
		Apple Address Book		Yahoo! Address Book
		-----------------------------------------------------------
		First Name				First
		Middle Name				Middle
		Last Name				Last
		Nickname				Nickname
		Group					Category
		-						Distribution Lists				
		Home e-mail				Email
		Work e-mail				Alternative Email 1
		Other e-mail				Alternative Email 2
		Home Phone				Home
		Work Phone				Work
		Mobile Phone				Mobile
		Other Phone				Other
		Pager Phone				Pager
		Work Fax				Fax
		Home Fax				-
		-						Yahoo! Mailbox
		-						Primary
		Home Page				Personal Web Site
		-						Company Web Site		
		Job Title					Title
		Department				-
		Company				Company	
		Work Street				Work Address
		Work City				Work City
		Work Province			Work State
		Work ZIP					Work ZIP
		Work Country				Work Country		
		Home Street				Home Street
		Home City				Home City
		Home Province			Home State
		Home ZIP				Home ZIP
		Home Country			Home Country		
		Birthday					Birthday
		-						Anniversary
		Note					Comments
		IM Yahoo					Messenger ID
		IM ICQ					Custom 1
		IM MSN					Custom 2
		IM AIM					Custom 3
		IM Jabber				Custom 4


Note: It is my real first Apple Script. Improvements are VERY welcome.


This script is useful for you? Consider making a PayPal donation to verde@aurelio.net.


*)


-- ##################################### USER CONFIG HERE

-- The resulting CSV file location (default is Desktop:Yahoo-AB.csv)
set CsvFile to (((path to desktop folder) as text) & "Yahoo-AB.csv")

-- Default values for fields not available in Address Book
set YahooDistributionList to "Unfiled"
set YahooMainPhone to "home"

-- Maximum number of contacts to export
set MaxContacts to 999

-- List with all the Yahoo CSV fields
-- If your language is not listed here, get the field names in the first line of your Yahoo generated CSV file.

-- English fields. The default.
set YahooFields to {"First", "Middle", "Last", "Nickname", "Email", "Category", "Distribution Lists", "Yahoo! ID", "Home", "Work", "Pager", "Fax", "Mobile", "Other", "Yahoo! Phone", "Primary", "Alternate Email 1", "Alternate Email 2", "Personal Website", "Business Website", "Title", "Company", "Work Address", "Work City", "Work State", "Work ZIP", "Work Country", "Home Address", "Home City", "Home State", "Home ZIP", "Home Country", "Birthday", "Anniversary", "Custom 1", "Custom 2", "Custom 3", "Custom 4", "Comments"}

-- Portuguese fields. Uncomment this if your account is from Yahoo! Brasil
--set YahooDistributionList to "Todos"
--set YahooFields to {"Nome", "&nbsp;", "Sobrenome", "Apelido", "E-mail", "Categoria", "Listas de distribui‹o", "ID Yahoo!", "Informa›es pessoais", "Trabalho", "Pager", "Fax", "Celular", "Outro", "Fone Yahoo!", "Principal", "E-mail alternativo 1", "E-mail alternativo 2", "Web site pessoal", "Website da empresa", "Cargo", "Empresa", "Endereo", "Cidade", "Estado", "CEP", "Pa’s", "Endereo residencial", "Cidade", "Estado", "CEP", "Pa’s", "Data de nascimento", "Anivers‡rio", "Complemento 1", "Complemento 2", "Complemento 3", "Complemento 4", "Coment‡rios"}

-- ##################################### END OF USER CONFIG

set ScriptName to "Export Contacts to Yahoo CSV"
set scriptVersion to "1.2"
set DefaultCsvFileName to "Yahoo-AB.csv"
set ContactCount to 0
set AllLines to {}

-- Join list items in a single string, quoted and comma separated
-- Example: "one","two","three"
on ListToCsvLine(theList)
	set AppleScript's text item delimiters to "\",\""
	set CsvLine to ("\"" & theList as text) & "\"" & return
	set AppleScript's text item delimiters to ""
	return CsvLine
end ListToCsvLine

-- Convert from date property to Yahoo "MM/DD/YYYY" format
on FormatDate(theDate)
	if theDate is missing value then return ""
	set d to day of theDate as text
	set y to year of theDate as text
	set m to month of theDate as integer
	set m to m as text -- no need to zeropad
	return m & "/" & d & "/" & y
end FormatDate

-- Normalize (format & escape) the value of a Contact's field
on FixIt(theValue)
	
	-- XXX: dirty hack, don't know how to check if a variable is not defined
	try
		set FOO to theValue
	on error
		set theValue to ""
	end try
	
	-- Map missing to empty
	if theValue is missing value then Â
		set theValue to ""
	
	-- From here, we only know about text
	if class of theValue is not text then Â
		set theValue to theValue as text
	
	-- Every embedded quote is doubled ("->"") on Yahoo CSV
	if theValue contains "\"" then
		set AppleScript's text item delimiters to "\""
		set tmp_list to every text item of theValue
		set AppleScript's text item delimiters to "\"\""
		set theValue to tmp_list as string
	end if
	
	return theValue
end FixIt

-- Gives an alternate file name if the given one already exists
on AvailableFilePath(FilePath, DefaultFileName)
	if DefaultFileName is "" then set DefaultFileName to "a"
	
	tell application "Finder"
		-- If user defined a folder, set the default filename inside it
		if folder FilePath exists then Â
			set FilePath to (folder FilePath as text) & DefaultFileName
		
		if not (file FilePath exists) then
			-- File not found, the name is vacant and we're done			
			return FilePath
		else
			-- Name unavailable :(
			set DirName to container of file FilePath as text
			set BaseName to name of file FilePath
			set ExtName to name extension of file FilePath
		end if
	end tell
	
	-- Get basename without extension
	if ExtName is not "" then
		set AppleScript's text item delimiters to "."
		set BaseName to text items 1 thru -2 of BaseName as text
		set AppleScript's text item delimiters to ""
	end if
	
	-- Add the count at the end (-1, -2, ...)
	repeat with i from 1 to 99
		-- Compose the new name (with count and extension - if any)
		set NewBaseName to BaseName & "-" & i as text
		if ExtName is not "" then Â
			set NewBaseName to NewBaseName & "." & ExtName
		-- If it's free, we've found the new name
		tell application "Finder" to Â
			if not ((file NewBaseName exists) or (folder NewBaseName exists)) then exit repeat
	end repeat
	
	return DirName & NewBaseName
end AvailableFilePath

-- Write the CSV file on the disk
on WriteFile(FileData, FilePath)
	try
		set FilePath to FilePath as text
		set fd to open for access file FilePath with write permission
		write FileData as Unicode text to the fd starting at 0
		close access the fd
		return true
	on error
		try
			close access file FilePath
		end try
		return false
	end try
end WriteFile

-- Extract all information of a Contact, in the right CSV order, and save to a list
on ContactToYahooList(theContact, theGroup)
	set buf to {}
	tell application "Address Book"
		tell theContact
			
			-- Add the full name & nick
			set end of buf to my FixIt(first name)
			set end of buf to my FixIt(middle name)
			set end of buf to my FixIt(last name)
			set end of buf to my FixIt(nickname)
			
			-- Home e-mail
			set end of buf to my FixIt(value of first email where label is "home")
			
			-- Group
			if theGroup is "" then
				try
					set theGroup to (name of group of theContact) -- returns a list
					set theGroup to first item of theGroup -- if multiple, get the first
				end try
			end if
			set end of buf to my FixIt(theGroup)
			
			-- Distribution lists (not used in AB)
			set end of buf to my FixIt(my YahooDistributionList)
			
			-- IM: Yahoo
			try
				set end of buf to my FixIt(value of first Yahoo handle)
			on error
				set end of buf to ""
			end try
			
			-- Phone numbers
			if (count phones) > 0 then
				set end of buf to my FixIt(value of first phone where label is "home")
				set end of buf to my FixIt(value of first phone where label is "work")
				set end of buf to my FixIt(value of first phone where label is "pager")
				set end of buf to my FixIt(value of first phone where label is "work fax")
				set end of buf to my FixIt(value of first phone where label is "mobile")
				set end of buf to my FixIt(value of first phone where label is "other")
			else
				set buf to buf & {"", "", "", "", "", ""} -- 6 empty
			end if
			
			-- Yahoo phone and Main phone (not used in AB)
			set end of buf to ""
			set end of buf to my FixIt(my YahooMainPhone)
			
			-- Alternative e-mails
			set end of buf to my FixIt(value of first email where label is "work")
			set end of buf to my FixIt(value of first email where label is "other")
			
			-- Personal homepage
			set end of buf to my FixIt(home page)
			
			-- Work homepage (not used in AB)
			set end of buf to ""
			
			-- Job info
			set end of buf to my FixIt(job title)
			set end of buf to my FixIt(organization)
			
			-- Work and Home addresses (street, city, state, zip, country)
			if (count addresses) > 0 then
				set end of buf to my FixIt(street of first address where label is "work")
				set end of buf to my FixIt(city of first address where label is "work")
				set end of buf to my FixIt(state of first address where label is "work")
				set end of buf to my FixIt(zip of first address where label is "work")
				set end of buf to my FixIt(country of first address where label is "work")
				
				set end of buf to my FixIt(street of first address where label is "home")
				set end of buf to my FixIt(city of first address where label is "home")
				set end of buf to my FixIt(state of first address where label is "home")
				set end of buf to my FixIt(zip of first address where label is "home")
				set end of buf to my FixIt(country of first address where label is "home")
			else
				set buf to buf & {"", "", "", "", "", "", "", "", "", ""} -- 10 empty
			end if
			
			-- Birth date
			set end of buf to my FixIt(my FormatDate(birth date))
			
			-- Special date (not used in AB)
			set end of buf to ""
			
			-- IM: ICQ
			try
				set end of buf to my FixIt(value of first ICQ handle)
			on error
				set end of buf to ""
			end try
			
			-- IM: MSN
			try
				set end of buf to my FixIt(value of first MSN handle)
			on error
				set end of buf to ""
			end try
			
			-- IM: AIM
			try
				set end of buf to my FixIt(value of first AIM Handle)
			on error
				set end of buf to ""
			end try
			
			-- IM: Jabber
			try
				set end of buf to my FixIt(value of first Jabber handle)
			on error
				set end of buf to ""
			end try
			
			-- Notes
			set end of buf to my FixIt(note)
			
		end tell
	end tell
	log (buf)
	return buf
end ContactToYahooList

-- ###################################### Processing begins here

tell application "Address Book"
	
	-- First dialog: quit, all, group
	display dialog (ScriptName & " version " & (scriptVersion as text) & return & return & Â
		"Hi, I'm a robot. " & Â
		"My life's purpose is to convert your Address Book contacts to the Yahoo CSV format. " & Â
		"Then you can populate your Yahoo! Web account." & return & return & Â
		"It will take just a few seconds (with no progress indicator)." & return & return & Â
		"Do you want to convert all contacts or choose a specific group?" & return & return) Â
		buttons {"Cancel", "All Contacts", "Choose Group"} Â
		default button 3 with icon 1
	set UserActionChoice to button returned of the result
	
	-- Get information about the Book as a whole
	set allGroups to name of every group
	set TotalContacts to count person
	
	-- The user will choose a specific group
	if UserActionChoice is "Choose Group" then
		set UserGroup to choose from list allGroups with prompt Â
			"Choose one Address Book group to be exported:" OK button name "Export"
		if UserGroup is false then return -- User pressed Cancel button
		-- Get information about the chosen group
		set TotalContacts to count person of group named UserGroup
	else
		set UserGroup to ""
	end if
	
	-- It-will-take-time warning for large amount of contacts
	if TotalContacts > 100 then
		display dialog (ScriptName & return & return & Â
			"Wow! More than 100 contacts to export." & return & return & Â
			"Please be patient, this may take a while.") Â
			buttons {"OK"} default button 1 with icon 2 giving up after 6
	end if
	
	-- Sanity check
	if MaxContacts < TotalContacts then
		display dialog (ScriptName & return & return & Â
			"Warning: You have " & TotalContacts & " contacts to export, " & Â
			"but the current maximum is set to " & MaxContacts & "." & return & return & Â
			"To export all contacts, set the \"MaxContacts\" configuration to a higher value.") Â
			buttons {"Cancel", "Continue"} default button 1 with icon 2
	end if
	
	-- Add the field names to the first CSV line
	copy my ListToCsvLine(YahooFields) to the end of AllLines
	
	-- Query only the desired contacts
	if UserGroup is "" then
		set AllContacts to a reference to every person
	else
		set AllContacts to a reference to every person of group named UserGroup
	end if
	
	-- Here we go, extracting info of every contact and saving it to the AllLines buffer
	repeat with Contact in AllContacts
		if ContactCount is MaxContacts then exit repeat
		set ContactCount to ContactCount + 1
		
		set ContactInfo to my ContactToYahooList(Contact, UserGroup)
		copy my ListToCsvLine(ContactInfo) to the end of AllLines
	end repeat
	
end tell

-- Make sure we will not overwrite any file
set CsvFile to AvailableFilePath(CsvFile, DefaultCsvFileName)

-- Write the CSV file to the Desktop
set SaveOk to WriteFile(AllLines as text, CsvFile)

-- O-oh, file not saved :(
if not SaveOk then
	display dialog (ScriptName & return & return & Â
		"ERROR when trying to save the results to the following file:" & Â
		return & return & tab & tab & (CsvFile as text)) buttons {"OK"}
	return
end if

-- XXX: how to avoid these chars from being inserted?

-- Remove strange chars (^@ ^M) that are inserted on the CSV file
set CsvUnixFile to POSIX path of CsvFile
set TmpFile to AvailableFilePath(CsvUnixFile & ".tmp", "")
do shell script "cat " & CsvUnixFile & " | tr -d '\\000' | tr '\\015' '\\n' > " & TmpFile
do shell script "mv " & TmpFile & " " & CsvUnixFile

-- Great! CSV file successfully saved
display dialog (ScriptName & return & return & Â
	(ContactCount as text) & Â
	" contacts were exported from your Address Book to the following file:" & Â
	return & return & tab & tab & (CsvFile as text)) & return & return Â
	buttons {"Show in Finder", "Open in TextEdit", "Just Quit"} Â
	default button 3 with icon 1
set LastAction to button returned of result

-- Open the CSV file in Finder or TextEdit
-- Errors are cowardly ignored :)
try
	if LastAction is "Show in Finder" then
		tell application "Finder"
			activate
			reveal file CsvFile
		end tell
	else if LastAction is "Open in TextEdit" then
		tell application "TextEdit"
			launch
			activate
			open file CsvFile
		end tell
	end if
end try
